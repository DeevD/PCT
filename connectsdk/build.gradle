buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
//        classpath 'org.robolectric:robolectric-gradle-plugin:1.1.0'
    }
}

allprojects {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}

apply plugin: 'com.android.library'
//robolectric plugin causes build errors
//apply plugin: 'org.robolectric'
apply plugin: "jacoco"


jacoco {
    toolVersion = "0.7.1.201405082137"
}

task jacocoTestReport(type:JacocoReport, dependsOn: "check") {
    group = "Reporting"

    description = "Generate Jacoco coverage reports"

    classDirectories = fileTree(
            dir: 'build/intermediates/classes/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    )

    additionalSourceDirs = files(android.sourceSets.main.java.srcDirs)
    sourceDirectories = files(android.sourceSets.main.java.srcDirs)
    executionData = files('build/jacoco/testDebug.exec')

    reports {
        xml.enabled = true
        html.enabled = true
    }

}

build.dependsOn jacocoTestReport

android {

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22
    }
    compileSdkVersion rootProject.ext.compileSdk
    useLibrary 'org.apache.http.legacy'

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        pickFirst 'lib/x86_64/libjlibtorrent-1.2.0.16.so'

    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [
                    'src',
                    'core/src',
                    'modules/google_cast/src',
                    'modules/firetv/src',
            ]
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    
    lintOptions {
        abortOnError false
    }
}

android.testOptions.unitTests.all {
     include '**/*Test.class'
 }

dependencies {
    compile files('core/libs/java-websocket-patch.jar')
    compile files('core/libs/javax.jmdns_3.4.1-patch2.jar')

    compile fileTree(dir: 'modules/firetv/libs', include: '*.jar')

    compile "com.android.support:support-v4:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:appcompat-v7:${rootProject.ext.appCompatVersion}"
    compile "com.android.support:mediarouter-v7:${rootProject.ext.appCompatVersion}"
    compile 'com.google.android.gms:play-services-cast:9.8.0'

    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:2.4'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.powermock:powermock-api-mockito:1.6.2'
}
